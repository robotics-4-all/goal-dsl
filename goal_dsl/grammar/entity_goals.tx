import time_constraint
import condition
import entity
import utils


EntityGoal:
    EntityStateChangeGoal       |
    EntityStateConditionGoal    |
    EntityPyConditionGoal       |
    EntityAttrStreamGoal
;

EntityStateChangeGoal:
    'Goal<EntityStateChange>' name=ID
    (
        ('entity:' entity=[Entity:FQN|+m:entities*])
        ('timeConstraints:' '-' timeConstraints*=TimeConstraint['-'])?
    )#
    'end'
;

EntityStateConditionGoal:
    'Goal<EntityStateCondition>' name=ID
    (
        ('condition:' condition=Condition)
        ('timeConstraints:' '-' timeConstraints*=TimeConstraint['-'])?
    )#
    'end'
;

EntityPyConditionGoal:
    'Goal<EntityPyCondition>' name=ID
    (
        ('condition:' condition=STRING)
        ('timeConstraints:' '-' timeConstraints*=TimeConstraint['-'])?
    )#
    'end'
;

/*
EntityAttrStreamGoal:
    'Goal<EntityAttrStream>' name=ID
    (
        ('streams:' '-' streams+=AttrStreamDefinition['-'])
        // ('strategy:' strategy=[EntityAttrStreamStrategy])
        ('timeConstraints:' '-' timeConstraints*=TimeConstraint['-'])?
    )#
    'end'
;

AttrStreamDefinition:
    attr=[Attribute:FQN|+pm:entities.attributes*] ':' '[' values+=BASETYPE[','] ']' '->' strategy=[EntityAttrStreamStrategy]
;
*/


EntityAttrStreamGoal:
    'Goal<EntityAttrStream>' name=ID
    (
        ('stream:' stream=AttrStreamDefinition)
        ('strategy:' strategy=[EntityAttrStreamStrategy])
        ('timeConstraints:' '-' timeConstraints*=TimeConstraint['-'])?
    )#
    'end'
;


AttrStreamDefinition:
    attr=[Attribute:FQN|+pm:entities.attributes*] ':' '[' values+=BASETYPE[','] ']'
;

EntityAttrStreamStrategy:
    'ALL'               |
    'NONE'              |
    'AT_LEAST_ONE'      |
    'JUST_ONE'          |
    'EXACTLY_X'         |
	'ALL_ORDERED'       |
    'EXACTLY_X_ORDERED'
;


EntityStateTransitionGoal:
    'Goal<EntityStateTransition>' name=ID
    (
        ('entity:' entity=[Entity:FQN|+m:entities*])
        ('previous:' previous=Condition)
        ('next:' next=Condition)
        ('timeConstraints:' '-' timeConstraints*=TimeConstraint['-'])?
    )#
    'end'
;
