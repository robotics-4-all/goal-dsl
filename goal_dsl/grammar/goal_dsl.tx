/*
    Goal DSL
*/
import communication
import goal
import entity


GoalDSLModel:
    imports*=Import
    (metadata=Metadata)?
    (
        (brokers*=Broker)
        (entities*=Entity)
        (goals*=Goal)
        (scenarios*=Scenario)
    )#
;


Metadata:
    'Metadata:'
    (
        ('name:' name=ID)
        ('version:' version=STRING)
        ('author:' author=STRING)?
        ('email:' email=STRING)?
        ('description:' description=STRING)?
    )#
;


Scenario:
    'Scenario:'
    (
        ('name:' name=ID)
        ('goals:' '-' goals*=WeightedGoal['-'])
        ('concurrent:' concurrent=BOOL)?
        ('broker:' broker=[Broker:FQN|+m:brokers*])?
    )#
;

WeightedGoal:
    goal=[Goal:FQN|+m:goals*] ('->' weight=FLOAT)?
;

// Utilities ------------------------------------->

Import: 'import' importURI=FQN ('as' name=ID)?;

FQN: ID('.'ID)*;

// Special rule for comments. Comments start with //
Comment: CommentLine | CommentBlock ;

CommentLine: /\/\/.*?$/;

CommentBlock: /\/\*(.|\n)*?\*\//;

// <-----------------------------------------------
