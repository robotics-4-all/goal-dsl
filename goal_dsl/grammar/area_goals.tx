import entity
import data_types
import time_constraint


AreaGoal:
    RectangleAreaGoal     |
    CircularAreaGoal      |
    CrossStraightLineGoal |
    MovingAreaGoal        |
    PolylineAreaGoal
;


RectangleAreaGoal:
    'Goal<RectangleArea>:'
    (
        ('name:' name=ID)
        ('entity:' entity=[Entity:FQN|+pm:entities*])
        ('bottomLeftEdge:' bottomLeftEdge=Point )
        ('lengthX:' lengthX=NUMBER )
        ('lengthY:' lengthY=NUMBER )
        ('tag:' type=AreaGoalTag )
        ('timeConstraints:' '[' timeConstraints*=TimeConstraint[','] ']' )?
    )#  // Any order
;

PolylineAreaGoal:
    'Goal<PolylineArea>:'
    (
        ('name:' name=ID)
        ('entity:' entity=[Entity:FQN|+pm:entities*])
        ('points:' '[' points+=Point[','] ']' )
        ('tag:' type=AreaGoalTag )
        ('timeConstraints:' '[' timeConstraints*=TimeConstraint[','] ']' )?
    )#  // Any order
;

MovingAreaGoal:
    'Goal<MovingArea>:'
    (
        ('name:' name=ID)
        ('entity:' entity=[Entity:FQN|+pm:entities*])
        ('radius:' radius=NUMBER )
        ('tag:' type=AreaGoalTag )
        ('timeConstraints:' '[' timeConstraints*=TimeConstraint[','] ']' )?
    )#  // Any order
;

CircularAreaGoal:
    'Goal<CircularArea>:'
    (
        ('name:' name=ID)
        ('entity:' entity=[Entity:FQN|+pm:entities*])
        ('center:' center=Point)
        ('radius:' radius=NUMBER)
        ('tag:' type=AreaGoalTag)
        ('timeConstraints:' '[' timeConstraints*=TimeConstraint[','] ']')?
    )#
;

CrossStraightLineGoal:
    'Goal<CrossStraightLine>:'
    (
        ('name:' name=ID)
        ('entity:' entity=[Entity:FQN|+pm:entities*])
        ('startPoint:' startPoint=Point)
        ('endPoint:' endPoint=Point)
        ('tag:' type=AreaGoalTag)
        ('timeConstraints:' '[' timeConstraints*=TimeConstraint[','] ']' )?
    )#
;

AreaGoalTag: 'ENTER' | 'EXIT' | 'AVOID' | 'STEP';

FQN: ID('.'ID)*;
