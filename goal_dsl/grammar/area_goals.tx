import entity
import data_types
import time_constraint


AreaGoal:
    RectangleAreaGoal     |
    CircularAreaGoal      |
    CrossStraightLineGoal |
    MovingAreaGoal        |
    PolylineAreaGoal
;


RectangleAreaGoal:
    'Goal<RectangleArea>' name=ID
    (
        ('entity:' entity=[Entity|FQN])
        ('bottomLeftEdge:' bottomLeftEdge=Point )
        ('lengthX:' lengthX=NUMBER )
        ('lengthY:' lengthY=NUMBER )
        ('tag:' type=AreaGoalTag )
        ('timeConstraints:' '[' timeConstraints*=TimeConstraint[','] ']' )?
    )#  // Any order
    'end'
;

PolylineAreaGoal:
    'Goal<PolylineArea>' name=ID
    (
        ('entity:' entity=[Entity|FQN] )
        ('points:' '[' points+=Point[','] ']' )
        ('tag:' type=AreaGoalTag )
        ('timeConstraints:' '[' timeConstraints*=TimeConstraint[','] ']' )?
    )#  // Any order
    'end'
;

MovingAreaGoal:
    'Goal<MovingArea>' name=ID
    (
        ('entity:' entity=[Entity|FQN] )
        ('radius:' radius=NUMBER )
        ('tag:' type=AreaGoalTag )
        ('timeConstraints:' '[' timeConstraints*=TimeConstraint[','] ']' )?
    )#  // Any order
    'end'
;

CircularAreaGoal:
    'Goal<CircularArea>' name=ID
    (
        ('entity:' entity=[Entity|FQN] )
        ('center:' center=Point )
        ('radius:' radius=NUMBER )
        ('tag:' type=AreaGoalTag )
        ('timeConstraints:' '[' timeConstraints*=TimeConstraint[','] ']' )?
    )#
    'end'
;

CrossStraightLineGoal:
    'Goal<CrossStraightLine>' name=ID
    (
        ('entity:' entity=[Entity|FQN] )
        ('startPoint:' startPoint=Point )
        ('endPoint:' endPoint=Point )
        ('tag:' type=AreaGoalTag )
        ('timeConstraints:' '[' timeConstraints*=TimeConstraint[','] ']' )?
    )#
    'end'
;

AreaGoalTag: 'ENTER' | 'EXIT' | 'AVOID' | 'STEP';

FQN: ID('.'ID)*;
