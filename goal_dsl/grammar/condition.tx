import entity
import types
import utils

// === Conditions ===

Condition:
    r1=ConditionGroup (operator=LogicalOperator r2=ConditionGroup)*
;

ConditionGroup:
    '(' r=Condition ')'
    | PrimitiveCondition
    | MathExpression
;

PrimitiveCondition:
    BoolCondition       |
    TimeCondition       |
    NumericCondition    |
    StringCondition     |
    ListCondition       |
    DictCondition
;

MathExpression:
    left=MathTerm (operator=PlusOrMinus right=MathTerm)*
;

MathTerm:
    left=MathFactor (operator=MulOrDiv right=MathFactor)*
;

MathFactor:
    (sign=PlusOrMinus)?
    (
        op=MathOperand | '(' op=MathExpression ')'
    )
;

MathOperand:
(
    op=NUMBER               |
    op=AugmentedNumericAttr |
    op=AttributeRef
)
;

AttributeRef:
    attribute=[Attribute:FQN|+pm:entities.attributes]
;

PlusOrMinus: '+' | '-';
MulOrDiv: '*' | '/' ;

NumericCondition:
    (operand1=MathExpression
     operator=NumericOperator
     operand2=MathExpression)
;

StringCondition:
    (operand1=AugmentedStringAttr
     operator=StringOperator
     (operand2=STRING | operand2=AugmentedStringAttr))
;

BoolCondition:
    (operand1=AugmentedBoolAttr
     operator=BooleanOperator
     (operand2=BOOL | operand2=AugmentedBoolAttr))
;

ListCondition:
    (operand1=AugmentedListAttr
     operator=ListOperator
     (operand2=List | operand2=AugmentedListAttr))
;

DictCondition:
    (operand1=AugmentedDictAttr
     operator=DictOperator
     (operand2=Dict | operand2=AugmentedDictAttr))
;

TimeCondition:
    (operand1=SimpleTimeAttr
     operator=TimeOperator
     operand2=Time)
;

StdAttr:
    'std' '(' attribute=AugmentedNumericAttr ',' size=INT ')'
;

VarAttr:
    'var' '(' attribute=AugmentedNumericAttr ',' size=INT ')'
;

MeanAttr:
    'mean' '(' attribute=AugmentedNumericAttr  ',' size=INT ')'
;

MinAttr:
    'min' '(' attribute=AugmentedNumericAttr  ',' size=INT ')'
;

MaxAttr:
    'max' '(' attribute=AugmentedNumericAttr  ',' size=INT ')'
;

InRangeAttr:
    'in_range' '(' attribute=AugmentedNumericAttr  ',' min=NUMBER ',' max=NUMBER ')'
;


AugmentedAttr:
    AugmentedNumericAttr |
    AugmentedStringAttr  |
    AugmentedBoolAttr    |
    AugmentedListAttr    |
    AugmentedDictAttr
;


AugmentedNumericAttr:
    StdAttr         |
    VarAttr         |
    MeanAttr        |
    MinAttr         |
    MaxAttr         |
    InRangeAttr     |
    SimpleNumericAttr
;

AugmentedStringAttr: SimpleStringAttr;

AugmentedBoolAttr: SimpleBoolAttr;

AugmentedListAttr: SimpleListAttr;

AugmentedDictAttr: SimpleDictAttr;

SimpleNumericAttr:
    attribute=[NumericAttribute:FQN|+pm:entities.attributes]
;

SimpleStringAttr:
    attribute=[StringAttribute:FQN|+pm:entities.attributes]
;

SimpleBoolAttr:
    attribute=[BoolAttribute:FQN|+pm:entities.attributes]
;

SimpleListAttr:
    attribute=[ListAttribute:FQN|+pm:entities.attributes]
;

SimpleDictAttr:
    attribute=[DictAttribute:FQN|+pm:entities.attributes]
;

SimpleTimeAttr:
    attribute=[TimeAttribute:FQN|+pm:entities.attributes]
;

// Operators

// SharedOperator: '==' | '!=' | 'is' | 'is not';

StringOperator: '~' | '!~' | '==' | '!=' | 'is not' | 'is' | 'has' | 'in' | 'not in';

NumericOperator: '>=' | '>' | '<=' | '<' | '==' | '!=' | 'is not' | 'is';

LogicalOperator: 'and' | 'or' | 'not' | 'xor' | 'nor' | 'xnor' | 'nand';

BooleanOperator: '==' | '!=' | 'is not' | 'is';

ListOperator: '==' | '!=' | 'is' | 'is not' | 'in' | 'not in';

DictOperator: '==' | '!=' | 'is' | 'is not';

TimeOperator: '>=' | '>' | '<=' | '<' | '==' | '!=' | 'is not' | 'is';
